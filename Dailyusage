AzureDiagnostics
| where Category == "PostgreSQLLogs"
| where Message has "duration:"
| parse Message with * "duration: " duration:double " ms" *
| where duration > 1000
| project TimeGenerated, duration, Message
| sort by duration desc


AzureDiagnostics
| where Category == "PostgreSQLLogs"
| where Message has "deadlock detected"
| project TimeGenerated, Message

AzureDiagnostics
| where Category == "PostgreSQLLogs"
| parse Message with Operation:string " " *
| where Operation in ("select", "insert", "update", "delete")
| summarize QueryCount = count() by Operation, bin(TimeGenerated, 1m)

AzureDiagnostics
| where Category == "PostgreSQLSessions"
| summarize SessionCount = count() by bin(TimeGenerated, 5m)


AzureDiagnostics
| where Category == "PostgreSQLLogs"
| where Message startswith "duration:"
| extend DurationMs = todouble(extract(@"duration: ([0-9\.]+)", 1, Message))
| extend Statement = extract(@"statement: (.*)", 1, Message)
| extend Operation = tostring(split(trim(" ", Statement), " ")[0])
| where isnotempty(Operation) and DurationMs > 5000
| summarize LongRunningQueryCount = count() by Operation, bin(TimeGenerated, 1m)
| order by TimeGenerated desc

AzureDiagnostics
| where Category == "PostgreSQLLogs"
| where isnotempty(Message)
| extend QueryType =
    case(
        Message has "SELECT", "SELECT",
        Message has "INSERT", "INSERT",
        Message has "UPDATE", "UPDATE",
        Message has "DELETE", "DELETE",
        Message has "CREATE", "CREATE",
        Message has "DROP",   "DROP",
        Message has "ALTER",  "ALTER",
        "UNKNOWN"
    )
| where QueryType != "UNKNOWN"
| summarize QueriesPerMinute = count() by bin(TimeGenerated, 1m), QueryType
| order by TimeGenerated desc
